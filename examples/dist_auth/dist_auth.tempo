struct Credentials {
  Username: String;
  Password: String;
}

struct@(C,S) AuthResult {
  Success: Bool@[C,S];
  Token: String@[C,S];
}

interface ClientRegistry {
  func GetSalt(username: String) String;
  func Check(hash: String) Bool;
}

interface TokenGenerator {
  func GenerateToken() String;
}

interface Hasher {
  func CalcHash(salt: String, password: String) String;
}

func@(Client, Service, IP) Authenticate(
  credentials: Credentials@Client,
  registry: ClientRegistry@IP,
  tokenGen: TokenGenerator@IP,
  hasher: Hasher@Client
) AuthResult@(Client, Service) {
  let username: async String@IP = Client -> IP credentials.Username;
  let salt: async String@Client = IP -> Client registry.GetSalt(await username);

  let hash: async String@IP = Client->IP hasher.CalcHash(await salt, credentials.Password);
  let valid = IP -> [Client, Service] registry.Check(await hash);

  if await valid {
    let token: async String@[Client, Service] = IP -> [Client, Service] tokenGen.GenerateToken();
    return AuthResult@(Client, Service) { Success: true, Token: await token };
  } else {
    return AuthResult@(Client, Service) { Success: false, Token: "" };
  }
}
